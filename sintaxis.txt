; Insensible a mayúsculas y minúsculas
; Notación BNF
; <_> significa espacio en blanco (cualquier tipo de espacio en blanco), o un comentario
; Algunos de los espacios son opcionales, allá donde no haya ambiguedad:
; por ejemplo: a.b o a<-b


; Reglas gramaticales
; nombreSimple expresión regular ([A-Za-záéíóúÁÉÍÓÚñÑ][A-Za-záéíóúÁÉÍÓÚñÑ0-9]*)
; numero expresión regular ((?:[0-1]+(?:\|2))|(?:[0-9A-Fa-f]+(?:\|16))|(?:[0-9]+(?:\|10)?))
; texto expresión regular \"([^"\\]|\\.|\\\n)*\"
; letra expresión regular \'([^"\\]|\\.|\\\n)*\'
; comentario expresión regular \/\/.*

; Nota, para las expresiones del mismo nivel,
; el arbol que se construye se reordena para evaluar primero a la izquierda.

<modulo> ::= [<configuraciones> <_>] <subrutina>+

<configuraciones> ::= "configuracion" <_> (<configuracion> <_>)* "fin"

<configuracion> ::= "importar" <_> <texto> <_> "como" <nombre>
    | <nombre> <_> "<-" <_> <numero>
    | <nombre> <_> "<-" <_> <texto>

<subrutina>  ::= <subrutinaCabecera> <_> <subrutinaCuerpo>

<subrutinaCabecera>  ::= "subrutina" (<_> <modificador>)* [<_> <nombre>]

<subrutinaCuerpo>  ::= <datos> <_> <algoritmo> <_> "fin"

<modificador> ::= "externa"
    | "es"
    | "rápida"
    | "rapida"

<nombre> ::= <nombreSimple> (<_> "." <_> <nombre>)*

<datos> ::= "datos" <_> (<declaracion> <_>)+

<algoritmo> ::= "algoritmo" <_> (<sentencia> <_>)+

<declaracion> ::= <nombre> <_> "es" <_> <nombre> (<_> <declaracionModificador>)*
    | <nombre> <_> "es" <_> "lista" "(" <_> <listaDecl> <_> ")" <_> "de" <_> <nombre> (<_> <declaracionModificador>)*
    | <nombre> <_> "es" <_> "relacion" <_> "de" <_> <nombre> <_> "a" <_>  (<_> <declaracionModificador>)*

<listaDecl> ::= <rango> <_> "," <_> <listaDecl>
    | <rango>

<rango> ::= <numero> <_> ".." <_> <numero>

<sentencia> ::= <asignacion>
    | <llamada>
    | <repetir>
    | <sicondicional>
    | <mientras>
    | "pausar"

<declaracionModificador> ::= "de" <_> "entrada"
    | "de" <_> "salida"
    | "global"

<asignacion> ::= <nombre> <_> "<-" <_> <expresion>
    | <nombre> <_> "(" <_> <listaAcceso> <_> ")" <_> "<-" <_> <expresion>

<llamada> ::= <nombre> <_> "->" <_> <nombre> <_> "->" <_> <nombre>
    | <nombre> <_> "->" <_> <nombre>
    | <nombre> <_> "[" <_> (<llamadaAsignacion> <_>)+ "]"

<repetir> ::= "repetir" <_> <expresion> <_> "veces" <_> (<sentencia> <_>)+ "fin"

<mientras> ::= "mientras" <_> <expresion> <_> "hacer" (<sentencia> <_>)+ "fin"

<sicondicional> ::= "si" <_> <expresion> <_> "hacer" <_> (<sentencia> <_>)+ "fin"
    | "si" <_> <expresion> <_> "hacer" <_> (<sentencia> <_>)+ <sinocondicional>
    | "si" <_> <expresion> <_> "hacer" <_> (<sentencia> <_>)+ <sino>

<sinocondicional> ::= "o" <_> "si" <_> <expresion> <_> "hacer" <_> (<sentencia> <_>)+ "fin"
    | "o" <_> "si" <expresion> <_> "hacer" <_> (<sentencia> <_>)+ <sinocondicional>
    | "o" <_> "si" <expresion> <_> "hacer" <_> (<sentencia> <_>)+ <sino>

<sino> ::= "si" <_> "no" <_> "hacer" <_> (<sentencia> <_>)+ "fin"

<llamadaAsignacion> ::= <expresion> <_> "->" <_> <nombre>
    | <nombre> <_> "<-" <_> <expresion>

<expresion> ::= <expresionTercera> <_> <operadorBinarioCuarto> <_> <expresion>
    | <expresionTercera>

<expresionTercera> ::= <expresionSegunda> <_> <operadorBinarioTercero> <_> <expresionTercera>
    | <expresionSegunda>

<expresionSegunda> ::= <expresionPrimera> <_> <operadorBinarioSegundo> <_> <expresionSegunda>
    | <expresionPrimera>

<expresionPrimera> ::= <operadorUnario> <_> <expresion>
    | <evaluacion> <_> <operadorBinarioPrimero> <_> <expresionPrimera>
    | <evaluacion>

<evaluacion> ::= <numero>
    | <texto>
    | <letra>
    | "verdadero"
    | "falso"
    | <nombre> <_> "como" <_> <nombre>
    | <nombre> <_> "(" <_> <listaAcceso> <_> ")"
    | <nombre>
    | "(" <_> <expresion> <_> ")"

<listaAcceso> ::= <rango> <_> "," <_> <listaAcceso>
    | <expresion> <_> "," <_> <listaAcceso>
    | <rango>
    | <expresion>

<operadorUnario> ::= "-"
    | "+"
    | "no"

<operadorBinarioCuarto> ::= "y"
    | "o"

<operadorBinarioTercero> ::= "<"
    | ">"
    | "<="
    | ">="
    | "="
    | "<>"

<operadorBinarioSegundo> ::= "+"
    | "-"

<operadorBinarioPrimero> ::= "*"
    | "/"
    | "%"
